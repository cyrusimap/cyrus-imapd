#!perl
use Cassandane::Tiny;

sub test_calendarevent_set_preserve_icalxprops
    :min_version_3_9 :needs_component_jmap
{
    my ($self) = @_;
    my $jmap = $self->{jmap};
    my $caldav = $self->{caldav};

    xlog $self, "Create event with unknown x-properties";

    my $mainevent_xprop = "X-MAINEVENT-PROP;X-FOO=1:foo";
    my $recurevent_xprop = "X-RECUREVENT-PROP;X-BAR=2:bar";
    my $alarm_xprop = "X-ALARM-PROP;X-BAZ=3:baz";

    my $ical = <<EOF;
BEGIN:VCALENDAR
VERSION:2.0
PRODID:-//Apple Inc.//Mac OS X 10.9.5//EN
CALSCALE:GREGORIAN
BEGIN:VEVENT
DTSTART:20230309T160000Z
DURATION:PT1H
RRULE:FREQ=DAILY
UID:7F81A15D-9973-4BD7-AACA-FB15F20CD815
SUMMARY:test
$mainevent_xprop
BEGIN:VALARM
UID:E576FA39-51B6-45FA-AAF3-7CCE1F21802E
ACTION:DISPLAY
TRIGGER:-PT5M
$alarm_xprop
END:VALARM
END:VEVENT
BEGIN:VEVENT
RECURRENCE-ID:20230310T160000Z
DTSTART:20230310T160000Z
DURATION:PT1H
RRULE:FREQ=DAILY
UID:7F81A15D-9973-4BD7-AACA-FB15F20CD815
SUMMARY:test
$recurevent_xprop
END:VEVENT
END:VCALENDAR
EOF
    my $res = $caldav->Request('PUT',
        '/dav/calendars/user/cassandane/Default/test.ics',
        $ical, 'Content-Type' => 'text/calendar');

    $res = $jmap->CallMethods([
        ['CalendarEvent/get', { }, 'R1']
    ]);
    my $eventId = $res->[0][1]{list}[0]{id};
    $self->assert_not_null($eventId);

    xlog $self, "Update some event property via JMAP";
    $res = $jmap->CallMethods([
        ['CalendarEvent/set', {
            update => {
                $eventId => {
                    title => 'test2',
                },
            },
        }, 'R1'],
    ]);
    $self->assert(exists $res->[0][1]{updated}{$eventId});

    xlog $self, "Assert iCalendar still contains x-properties";

    $res = $caldav->Request('GET', 'Default/test.ics');
    $ical = Data::ICal->new(data => $res->{content});

    my $mainevent = (grep {
            ($_->ical_entry_type() eq 'VEVENT') and (scalar $_->property('RRULE'))
    } @{$ical->entries()})[0];
    $self->assert_matches(qr/$mainevent_xprop/, $mainevent->as_string());

    my $valarm = (grep {
            ($_->ical_entry_type() eq 'VALARM')
    } @{$mainevent->entries()})[0];
    $self->assert_matches(qr/$alarm_xprop/, $valarm->as_string());

    my $recurevent = (grep {
            ($_->ical_entry_type() eq 'VEVENT') and (scalar $_->property('RECURRENCE-ID'))
    } @{$ical->entries()})[0];
    $self->assert_matches(qr/$recurevent_xprop/, $recurevent->as_string());
}
