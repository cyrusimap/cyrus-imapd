#!perl
use Cassandane::Tiny;

sub test_recurring_absolute_trigger
    :min_version_3_7
{
    my ($self) = @_;

    my $CalDAV = $self->{caldav};

    my $CalendarId = $CalDAV->NewCalendar({name => 'foo'});
    $self->assert_not_null($CalendarId);

    my $now = DateTime->now();
    $now->set_time_zone('Australia/Sydney');
    if ($now->hour >= 1 && $now->hour <= 4) {
      # Get us away from DST cutover which breaks the test
      $now->add(DateTime::Duration->new(hours => 4));
    }
    # bump everything forward so a slow run (say: valgrind)
    # doesn't cause things to magically fire...
    $now->add(DateTime::Duration->new(seconds => 300));

    # define the event to start in a few seconds
    my $startdt = $now->clone();
    $startdt->add(DateTime::Duration->new(seconds => 2));
    my $start = $startdt->strftime('%Y%m%dT%H%M%SZ');

    my $enddt = $startdt->clone();
    $enddt->add(DateTime::Duration->new(seconds => 15));
    my $end = $enddt->strftime('%Y%m%dT%H%M%SZ');

    # set the trigger to notify us at the start of the event
    my $trigger = $startdt->strftime('%Y%m%dT%H%M%SZ');

    # calculate start time for second instance
    my $recuriddt = $startdt->clone();
    $recuriddt->add(DateTime::Duration->new(days => 1));
    my $recurid = $recuriddt->strftime('%Y%m%dT%H%M%SZ');

    my $uuid = "574E2CD0-2D2A-4554-8B63-C7504481D3A9";
    my $href = "$CalendarId/$uuid.ics";
    my $card = <<EOF;
BEGIN:VCALENDAR
VERSION:2.0
PRODID:-//Apple Inc.//Mac OS X 10.10.4//EN
CALSCALE:GREGORIAN
BEGIN:VEVENT
CREATED:20150806T234327Z
UID:574E2CD0-2D2A-4554-8B63-C7504481D3A9
DTEND:$end
TRANSP:OPAQUE
SUMMARY:Simple
DTSTART:$start
DTSTAMP:20150806T234327Z
SEQUENCE:0
RRULE:FREQ=DAILY;COUNT=3
BEGIN:VALARM
TRIGGER;VALUE=DATE-TIME:$trigger
ACTION:DISPLAY
SUMMARY: My alarm
DESCRIPTION:My alarm has triggered
END:VALARM
BEGIN:VALARM
TRIGGER:PT0S
ACTION:EMAIL
SUMMARY: My alarm
DESCRIPTION:My alarm has triggered
END:VALARM
END:VEVENT
END:VCALENDAR
EOF

    $CalDAV->Request('PUT', $href, $card, 'Content-Type' => 'text/calendar');

    # clean notification cache
    $self->{instance}->getnotify();

    # adjust now to UTC
    $now->add(DateTime::Duration->new(seconds => $now->offset()));

    $self->{instance}->run_command({ cyrus => 1 }, 'calalarmd', '-t' => $now->epoch() - 60 );

    $self->assert_alarms();

    # fire alarms for first instance
    $self->{instance}->run_command({ cyrus => 1 }, 'calalarmd', '-t' => $now->epoch() + 60 );

    $self->assert_alarms({summary => 'Simple', start => $start, action => 'display'},
                         {summary => 'Simple', start => $start, action => 'email'});

    # fire alarm for second instance
    $self->{instance}->run_command({ cyrus => 1 }, 'calalarmd', '-t' => $now->epoch() + 86400 + 60 );

    $self->assert_alarms({summary => 'Simple', start => $recurid, action => 'email'});
}
