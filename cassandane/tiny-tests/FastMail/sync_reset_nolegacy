#!perl
use Cassandane::Tiny;

sub test_sync_reset_nolegacy
    :DelayedDelete :min_version_3_5 :NoMailboxLegacyDirs
    :needs_component_replication
    ($self)
{
    my $adminstore = $self->{adminstore};
    my $admintalk = $adminstore->get_client();

    my $inbox = "user.magicuser";
    my $subfolder = "$inbox.foo";

    $admintalk->create($inbox);
    $admintalk->setacl($inbox, admin => 'lrswipkxtecdan');
    $admintalk->create($subfolder);
    $self->assert_str_equals('ok', $admintalk->get_last_completion_response());

    $adminstore->set_folder($subfolder);
    $self->make_message("Email", store => $adminstore) or die;

    # Create the search database.
    xlog $self, "Run squatter";
    $self->{instance}->run_command({cyrus => 1}, 'squatter');

    # Reset the conv.db version to 1 (to force UUID-based MAILBOXIDs)
    my $dirs = $self->{instance}->run_mbpath(-u => 'magicuser');
    my $format = $self->{instance}->{config}->get('conversations_db');
    $self->{instance}->run_dbcommand($dirs->{user}{conversations}, $format,
                                     ['SET', '$VERSION', '1']);

    my $res = $admintalk->status($inbox, ['uniqueid']);
    my $inboxid = $res->{uniqueid}[0];
    $res = $admintalk->status($subfolder, ['uniqueid']);
    my $subid = $res->{uniqueid}[0];

    my $basedir = $self->{instance}{basedir};
    open(FH, "-|", "find", $basedir);
    my @files = grep { m{/uuid/} } <FH>;
    close(FH);

    xlog $self, "files exists";
    $self->assert_contains(qr{$inboxid}, \@files);
    $self->assert_contains(qr{$subid}, \@files);

    $self->{instance}->run_command({ cyrus => 1 }, 'sync_reset', '-f' => 'magicuser' );

    open(FH, "-|", "find", $basedir);
    @files = grep { m{/uuid/} } <FH>;
    close(FH);

    xlog $self, "ensure there's no files left matching either uuid!";
    $self->assert_not_contains(qr{$inboxid}, \@files);
    $self->assert_not_contains(qr{$subid}, \@files);
}
