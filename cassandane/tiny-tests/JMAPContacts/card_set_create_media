#!perl
use Cassandane::Tiny;

sub test_card_set_create_media
    :min_version_3_9 :needs_dependency_icalvcard
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';

    my $res = $jmap->CallMethods([
        ['ContactCard/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $id,
                    name => { full => 'Jane Doe' },
                    media => {
                        res45 => {
                            '@type' => 'MediaResource',
                            kind => 'sound',
                            uri => 'CID:JOHNQ.part8.19960229T080000.xyzMail@example.com'
                        },
                        res47 => {
                            '@type' => 'MediaResource',
                            kind => 'logo',
                            mediaType => 'image/jpeg',
                            uri => 'https://www.example.com/pub/logos/abccorp.jpg'
                        },
                        res1 => {
                            '@type' => 'MediaResource',
                            kind => 'photo',
                            uri => 'data:image/jpeg;base64,c29tZSBwaG90bw=='
                        }
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});
    my $blobId = $res->[0][1]{created}{1}{media}{res1}{blobId};
    $self->assert_not_null($blobId);

    my $href = $res->[0][1]{created}{1}{'cyrusimap.org:href'};
    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    $self->assert_matches(qr|LOGO;PROP-ID=res47;MEDIATYPE=image/jpeg:https://www.example.com/pub/logos/abccorp.jpg|, $card);
    $self->assert_matches(qr|SOUND(;VALUE=URI)?;PROP-ID=res45:CID:JOHNQ.part8.19960229T080000.xyzMail\@example.com|, $card);
    $self->assert_matches(qr|PHOTO;PROP-ID=res1:data:image/jpeg;base64,c29tZSBwaG90bw==|, $card);
    $self->assert_does_not_match(qr|JSPROP|, $card);

    $res = $jmap->Download('cassandane', $blobId);

    $self->assert_str_equals('image/jpeg', $res->{headers}{'content-type'});
    $self->assert_str_equals('some photo', $res->{content});
}
