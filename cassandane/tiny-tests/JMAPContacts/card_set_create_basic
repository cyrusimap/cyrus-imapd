#!perl
use Cassandane::Tiny;

sub test_card_set_create_basic
    :min_version_3_9 :needs_dependency_icalvcard
{
    my ($self) = @_;

    xlog $self, "create addressbook";
    my $abook = $self->default_user->addressbooks->create({ name => 'Foo' });

    my $now = DateTime->now();
    my $created = $now->strftime('%Y-%m-%dT%H:%M:%SZ');
    my $prodid = '-//Example Corp.//CardDAV Client//EN';
    my $name = 'Mr. John Q. Public, Esq.';
    my $uid = 'ae2640cc-234a-4dd9-95cc-3106258445b9';

    my $card = $abook->create_card({
        '@type' => 'Card',
        version => '1.0',
        uid => $uid,
        prodId => $prodid,
        kind => 'individual',
        created => $created,
        name => { full => $name }
    });

    $self->assert_not_null($card->id);

    my $vcard = $card->as_vcard4;

    $vcard =~ s/\r?\n[ \t]+//gs;  # unfold long properties
    $name =~ s/,/\\\\,/gs;        # escape commas

    $created = $now->strftime('%Y%m%dT%H%M%SZ'); # vCard doesn't use separators

    $self->assert_matches(qr/VERSION:4.0/, $vcard);
    $self->assert_matches(qr/KIND:INDIVIDUAL/, $vcard);
    $self->assert_matches(qr/UID;VALUE=TEXT:$uid/, $vcard);
    $self->assert_matches(qr/PRODID:$prodid/, $vcard);
    $self->assert_matches(qr/CREATED:$created/, $vcard);
    $self->assert_matches(qr/FN:$name/, $vcard);
    $self->assert_does_not_match(qr|JSPROP|, $vcard);
}
