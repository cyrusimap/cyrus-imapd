#!perl
use Cassandane::Tiny;

sub test_card_set_vcardprops
    :needs_dependency_icalvcard :MailboxLegacyDirs
    ($self)
{
    my $jmap = $self->{jmap};

    xlog $self, "Create card";
    my $res = $jmap->CallMethods([ [
        'ContactCard/set',
        {
            create => {
                1 => {
                    '@type' => 'Card',
                    name    => { full => 'Test1' },
                    emails  => {
                        email1 => {
                            "address" => 'test1@example.com',
                        }
                    },
                    onlineServices => {
                        os1 => {
                            uri => 'xmpp:test1@example.com',
                        },
                    }
                }
            }
        },
        'R1'
    ] ]);

    my $cardId1 = $res->[0][1]{created}{1}{id};
    $self->assert_not_null($cardId1);

    xlog $self, "Try to create/update card with vCard conversion properties (should fail)";
    $res = $jmap->CallMethods([ [
        'ContactCard/set',
        {
            create => {
                2 => {
                    '@type' => 'Card',
                    name    => { full => 'Test2' },
                    emails  => {
                        email1 => {
                            "address"     => 'test2@example.com',
                            "vCardParams" => { "x-foo" => "Bar" },
                        }
                    },
                    onlineServices => {
                        os1 => {
                            uri       => 'xmpp:test2@example.com',
                            vCardName => "impp",
                        },
                    },
                    vCardProps => [ [ "x-foo", {}, "unknown", "Hello" ] ],
                }
            },
            update => {
                $cardId1 => {
                    'emails/email1/vCardParams' => { "x-foo" => "Bar" },
                    'onlineServices/os1/vCardName' => 'impp',
                    vCardProps => [ [ "x-foo", {}, "unknown", "Hello" ] ],

                },
            },
        },
        'R1'
    ] ]);

    my @wantInvalidProps = sort ('vCardProps', 'emails/email1/vCardParams',
        'onlineServices/os1/vCardName',);

    $self->assert_not_null($res->[0][1]{notCreated}{2});
    $self->assert_str_equals('invalidProperties',
        $res->[0][1]{notCreated}{2}{type});
    my @gotInvalidProps = sort @{$res->[0][1]{notCreated}{2}{properties}};
    $self->assert_deep_equals(\@wantInvalidProps, \@gotInvalidProps);

    $self->assert_not_null($res->[0][1]{notUpdated}{$cardId1});
    $self->assert_str_equals('invalidProperties',
        $res->[0][1]{notUpdated}{$cardId1}{type});
    @gotInvalidProps = sort @{$res->[0][1]{notUpdated}{$cardId1}{properties}};
    $self->assert_deep_equals(\@wantInvalidProps, \@gotInvalidProps);
}
