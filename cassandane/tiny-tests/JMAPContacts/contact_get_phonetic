#!perl
use Cassandane::Tiny;

sub test_contact_get_phonetic
  : needs_component_jmap {
    my ($self)  = @_;
    my $jmap    = $self->{jmap};
    my $carddav = $self->{carddav};

    my $card = decode('utf-8', <<EOF
BEGIN:VCARD
VERSION:3.0
UID:E1951B19-80F3-4CA8-BB76-A887843858A3
FN:鈴木 一朗
N:鈴木;一朗;;;
X-PHONETIC-FIRST-NAME:いちろう
X-PHONETIC-LAST-NAME:すずき
ORG:日本野球機構
X-PHONETIC-ORG:にっぽんやきゅうきこう
END:VCARD
EOF
    );
    $card =~ s/\r?\n/\r\n/gs;
    $carddav->Request(
        'PUT', 'Default/test.vcf', $card,
        'Content-Type' => 'text/vcard; charset=utf-8'
    );

    my %vcardProps = (
        firstName         => decode('utf-8', '一朗'),
        lastName          => decode('utf-8', '鈴木'),
        company           => decode('utf-8', '日本野球機構'),
        phoneticFirstName => decode('utf-8', 'いちろう'),
        phoneticLastName  => decode('utf-8', 'すずき'),
        phoneticCompany   => decode('utf-8', 'にっぽんやきゅうきこう'),
    );

    my $res = $jmap->CallMethods([ [
        'Contact/get',
        {
            properties => [ keys %vcardProps ],
        },
        'R1'
    ] ]);

    keys %vcardProps;
    while (my ($prop, $value) = each %vcardProps) {
        $self->assert_str_equals($value, $res->[0][1]{list}[0]{$prop});
    }
}
