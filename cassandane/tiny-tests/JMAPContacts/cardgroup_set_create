#!perl
use Cassandane::Tiny;

sub test_cardgroup_set_create
    :min_version_3_9
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $name = 'The Doe Family';
    my $member1 = "urn:uuid:03a0e51f-d1aa-4385-8a53-e29025acd8af";
    my $member2 = "urn:uuid:\"b8767877-b4a1-4c70-9acc-505d3819e519\"";
    my $member3 = "foo\"bar^'baaazz4";
    my $uid = 'ae2640cc-234a-4dd9-95cc-3106258445b9';
    my $urn = "urn:uuid:$uid";

    my $res = $jmap->CallMethods([
        ['ContactCard/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $urn,
                    kind => 'group',
                    name => { full => $name },
                    members => {
                        $member1 => JSON::true,
                        $member2 => JSON::true,
                        $member3 => JSON::true
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});

    my $href = $res->[0][1]{created}{1}{'cyrusimap.org:href'};
    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    $self->assert_matches(qr/VERSION:4.0/, $card);
    $self->assert_matches(qr/KIND:GROUP/, $card);
    $self->assert_matches(qr/UID:$urn/, $card);
    $self->assert_matches(qr/FN:$name/, $card);
    $self->assert_matches(qr/MEMBER:$member1/, $card);
    $self->assert_matches(qr/MEMBER:$member2/, $card);
    $self->assert_matches(qr/MEMBER:\Q$member3/, $card);
    $self->assert_does_not_match(qr/N:;/, $card);

    $res = $carddav->Request('GET', $href, '');

    $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    # RFC6868-encode member strings
    $member2 =~ s/\^/\^\^/g; # ^ => ^^
    $member2 =~ s/"/\^'/g;   # " => ^'

    $member3 =~ s/\^/\^\^/g; # ^ => ^^
    $member3 =~ s/"/\^'/g;   # " => ^'

    $self->assert_matches(qr/VERSION:3.0/, $card);
    $self->assert_matches(qr/X-ADDRESSBOOKSERVER-KIND:group/i, $card);
    $self->assert_matches(qr/UID:$urn/, $card);
    $self->assert_matches(qr/FN:$name/, $card);
    $self->assert_matches(qr/X-ADDRESSBOOKSERVER-MEMBER:urn:uuid:"\Q$member1"/,
                          $card);
    $self->assert_matches(qr/X-ADDRESSBOOKSERVER-MEMBER:urn:uuid:"\Q$member2"/,
                          $card);
    $self->assert_matches(qr/X-ADDRESSBOOKSERVER-MEMBER:urn:uuid:\Q$member3/,
                          $card);
    $self->assert_does_not_match(qr/N:;/, $card);
}
