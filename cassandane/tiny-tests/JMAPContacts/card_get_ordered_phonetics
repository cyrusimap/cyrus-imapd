#!perl
use Cassandane::Tiny;
use utf8;

sub test_card_get_ordered_phonetics
    :min_version_3_9
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    # get id of Default addressbook
    my $res = $jmap->CallMethods([['AddressBook/get', {}, "R1"]]);
    my $abookId = $res->[0][1]{list}[0]{id};

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';
    my $href = "Default/test.vcf";
    my $card = <<EOF;
BEGIN:VCARD
VERSION:4.0
UID:$id
N;ALTID=n1;PHONETIC=IPA:/smɪθ/;/d͡ʒɑn/;;;;;
N;JSCOMPS=";1;0";ALTID=n1:Smith;John;;;;;
FN;DERIVED=TRUE:John Smith
CREATED:20230824T143619Z
END:VCARD
EOF
 
    $card =~ s/\r?\n/\r\n/gs;
    $carddav->Request('PUT', $href, $card, 'Content-Type' => 'text/vcard');

    $res = $jmap->CallMethods([
        ['ContactCard/get', {
        }, 'R1']
    ]);

    my $want_jscard = {
        '@type' => 'Card',
        version => '1.0',
        addressBookIds => { $abookId => JSON::true },
        'cyrusimap.org:href' => $carddav->fullpath() . $href,
        uid => $id,
        created => '2023-08-24T14:36:19Z',
        name => {
            phoneticSystem => 'ipa',
            isOrdered => JSON::true,
            components => [
                { kind => 'given',   value => 'John' , phonetic => "/d͡ʒɑn/" },
                { kind => 'surname', value => 'Smith', phonetic => "/smɪθ/" }
            ]
        },
    };

    my $have_jscard = $res->[0][1]{list}[0];

    # Delete generated fields
    delete $have_jscard->{id};
    delete $have_jscard->{blobId};
    delete $have_jscard->{'cyrusimap.org:blobId'};
    delete $have_jscard->{'cyrusimap.org:size'};

    # Normalize and compare cards
    normalize_jscard($want_jscard);
    normalize_jscard($have_jscard);

    $self->assert_deep_equals($want_jscard, $have_jscard);
}
