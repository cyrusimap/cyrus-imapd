#!perl
use Cassandane::Tiny;

sub test_card_set_create_phones
    :min_version_3_9
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';

    my $res = $jmap->CallMethods([
        ['ContactCard/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $id,
                    name => { full => 'Jane Doe' },
                    phones => {
                        tel0 => {
                            '@type' => 'Phone',
                            contexts => {
                                private => JSON::true
                            },
                            features => {
                                voice  => JSON::true,
                                mobile => JSON::true
                            },
                            number => 'tel:+1-555-555-5555;ext=5555',
                            pref => 1
                        },
                        tel3 => {
                            '@type' => 'Phone',
                            contexts => {
                                work => JSON::true
                            },
                            number => 'tel:+1-201-555-0123',
                            label => 'foo'
                        }
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});

    my $href = $res->[0][1]{created}{1}{'cyrusimap.org:href'};
    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    $self->assert_matches(qr/TEL;VALUE=URI;PROP-ID=tel0;TYPE=(CELL,VOICE|VOICE,CELL),HOME;PREF=1:tel:\+1-555-555-5555;ext=55/, $card);
    $self->assert_matches(qr/tel0.TEL;VALUE=URI;PROP-ID=tel3;TYPE=WORK:tel:\+1-201-555-0123/, $card);
    $self->assert_matches(qr/tel0.X-ABLabel:foo/, $card);
    $self->assert_does_not_match(qr|JSPROP|, $card);
}
