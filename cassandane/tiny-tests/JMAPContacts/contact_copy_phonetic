#!perl
use Cassandane::Tiny;

sub test_contact_copy_phonetic
  : needs_component_jmap {
    my ($self)    = @_;
    my $jmap      = $self->{jmap};
    my $carddav   = $self->{carddav};
    my $admintalk = $self->{adminstore}->get_client();
    my $service   = $self->{instance}->get_service("http");

    xlog $self, "create shared account";
    $admintalk->create("user.other");

    my $otherCarddav = Net::CardDAVTalk->new(
        user      => "other",
        password  => 'pass',
        host      => $service->host(),
        port      => $service->port(),
        scheme    => 'http',
        url       => '/',
        expandurl => 1,
    );

    my $otherJmap = Mail::JMAPTalk->new(
        user     => 'other',
        password => 'pass',
        host     => $service->host(),
        port     => $service->port(),
        scheme   => 'http',
        url      => '/jmap/',
    );
    $otherJmap->DefaultUsing([
        'urn:ietf:params:jmap:core',
        'https://cyrusimap.org/ns/jmap/contacts',
        'https://cyrusimap.org/ns/jmap/debug'
    ]);

    xlog $self, "share addressbook";
    $admintalk->setacl(
        "user.other.#addressbooks.Default",
        "cassandane" => 'lrswipkxtecdn'
    ) or die;

    my $card = decode(
        'utf-8', <<EOF
BEGIN:VCARD
VERSION:3.0
UID:0A8F88DE-1073-4D47-926F-0D535523FD15
N:Smith;Hank;Frank;
FN:Hank Smith
X-PHONETIC-FIRST-NAME:/hæŋk/
X-PHONETIC-MIDDLE-NAME:/fræŋk/
X-PHONETIC-LAST-NAME:/smɪθ/
REV:2008-04-24T19:52:43Z
END:VCARD
EOF
    );
    $card =~ s/\r?\n/\r\n/gs;
    $carddav->Request('PUT', 'Default/test.vcf', $card, 'Content-Type' => 'text/vcard');

    my $res = $jmap->CallMethods([
        [ 'Contact/query', {}, 'R1' ],
    ]);
    $self->assert_num_equals(1, scalar @{ $res->[0][1]{ids} });
    my $contactId = $res->[0][1]{ids}[0];
    $self->assert_not_null($contactId);

    $res = $jmap->CallMethods([
        [
            'Contact/copy',
            {
                fromAccountId => 'cassandane',
                accountId     => 'other',
                create        => {
                    contact1 => {
                        addressbookId => 'Default',
                        id            => $contactId
                    }
                },
                onSuccessDestroyOriginal => JSON::false,
            },
            'R1'
        ],
    ]);
    my $copiedContactId = $res->[0][1]{created}{contact1}{id};
    $self->assert_not_null($copiedContactId);

    $res = $otherJmap->CallMethods([
        [
            'Contact/get',
            {
                accountId  => 'other',
                ids        => [$copiedContactId],
                properties => [ 'phoneticFirstName', 'phoneticMiddleName', 'phoneticLastName' ],
            },
            'R1'
        ],
    ]);

    $self->assert_str_equals(decode('utf-8', '/hæŋk/'),  $res->[0][1]{list}[0]{phoneticFirstName});
    $self->assert_str_equals(decode('utf-8', '/fræŋk/'), $res->[0][1]{list}[0]{phoneticMiddleName});
    $self->assert_str_equals(decode('utf-8', '/smɪθ/'),  $res->[0][1]{list}[0]{phoneticLastName});
}
