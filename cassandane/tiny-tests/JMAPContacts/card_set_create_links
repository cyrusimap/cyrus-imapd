#!perl
use Cassandane::Tiny;

sub test_card_set_create_links
    :min_version_3_9
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';

    my $res = $jmap->CallMethods([
        ['ContactCard/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $id,
                    name => { full => 'Jane Doe' },
                    links => {
                        'CONTACT-1' => {
                            '@type' => 'LinkResource',
                            kind => 'contact',
                            uri => 'mailto:contact@example.com',
                            pref => 1
                        },
                        'LINK-1' => {
                            '@type' => 'LinkResource',
                            uri => 'https://example.org/restaurant.french/~chezchic.html'
                        }  
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});

    my $href = $res->[0][1]{created}{1}{'cyrusimap.org:href'};
    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    $self->assert_matches(qr|URL;PROP-ID=LINK-1:https://example.org/restaurant.french/~chezchic.html|, $card);
    $self->assert_matches(qr|CONTACT-URI;PROP-ID=CONTACT-1;PREF=1:mailto:contact\@example.com|, $card);
    $self->assert_does_not_match(qr|JSPROP|, $card);
}
