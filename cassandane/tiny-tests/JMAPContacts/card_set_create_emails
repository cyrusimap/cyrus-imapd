#!perl
use Cassandane::Tiny;

sub test_card_set_create_emails
    :min_version_3_9
    ($self)
{
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';

    my $res = $jmap->CallMethods([
        ['ContactCard/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $id,
                    name => { full => 'Jane Doe' },
                    emails => {
                        e1 => {
                            contexts => {
                                work => JSON::true
                            },
                            address => 'jqpublic@xyz.example.com'
                        },
                        e2 => {
                            '@type' => 'EmailAddress',
                            address => 'jane_doe@example.com',
                            pref => 1
                        }
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});

    my $href = $res->[0][1]{created}{1}{'cyrusimap.org:href'};
    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties

    $self->assert_matches(qr/EMAIL;PROP-ID=e1;TYPE=WORK:jqpublic\@xyz.example.com/, $card);
    $self->assert_matches(qr/EMAIL;PROP-ID=e2;PREF=1:jane_doe\@example.com/, $card);
    $self->assert_does_not_match(qr|JSPROP|, $card);
}
