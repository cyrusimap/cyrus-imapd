#!perl
use Cassandane::Tiny;

sub test_card_set_create_titles_org
    :min_version_3_9 :needs_component_jmap
{
    my ($self) = @_;
    my $jmap = $self->{jmap};

    my $service = $self->{instance}->get_service("http");
    $ENV{DEBUGDAV} = 1;
    my $carddav = Net::CardDAVTalk->new(
        user => 'cassandane',
        password => 'pass',
        host => $service->host(),
        port => $service->port(),
        scheme => 'http',
        url => '/',
        expandurl => 1,
    );

    my $id = 'ae2640cc-234a-4dd9-95cc-3106258445b9';
    my $href = "Default/$id.vcf";

    my $res = $jmap->CallMethods([
        ['Card/set', {
            create => {
                "1" => {
                    '@type' => 'Card',
                    version => '1.0',
                    uid => $id,
                    fullName => 'Jane Doe',
                    titles => {
                        'TITLE-1' => {
                            '@type' => 'Title',
                            kind => 'title',
                            name => 'Project Leader'
                        },
                        'TITLE-2' => {
                            '@type' => 'Title',
                            kind => 'role',
                            name => 'Research Scientist',
                            organization => 'ORG-1'
                        }
                    },
                    organizations => {
                        'ORG-1' => {
                            '@type' => 'Organization',
                            name => 'ABC, Inc.'
                        }
                    }
                }
            }
        }, 'R1']
    ]);

    $self->assert_not_null($res->[0][1]{created}{1});

    $res = $carddav->Request('GET', $href, '',
                             'Accept' => 'text/vcard; version=4.0');

    my $card = $res->{content};
    $card =~ s/\r?\n[ \t]+//gs;  # unfold long properties
    $card =~ s/\\,/,/gs;         # unescape commas

    $self->assert_matches(qr/TITLE;PROP-ID=TITLE-1:Project Leader/, $card);
    $self->assert_matches(qr/ORG-1.ROLE;PROP-ID=TITLE-2:Research Scientist/, $card);
    $self->assert_matches(qr/ORG-1.ORG;PROP-ID=ORG-1:ABC, Inc./, $card);
    $self->assert_does_not_match(qr|JSCONTACT-PROP|, $card);
}
