#!perl
use Cassandane::Tiny;

sub test_email_query_number
{
    my ($self) = @_;
    my $jmap   = $self->{jmap};
    my $imap   = $self->{store}->get_client();

    my $mime = <<'EOF';
From: from@example.com
To: to@example.com
Subject: test 9876
Date: Mon, 13 Apr 2020 15:34:03 +0200
MIME-Version: 1.0
Content-Type: text/plain

The total amount is $1,234.56 USD.
EOF
    $mime =~ s/\r?\n/\r\n/gs;
    $imap->append('INBOX', $mime) || die $@;

    xlog "Run squatter";
    $self->{instance}->run_command({ cyrus => 1 }, 'squatter', '-Z');

    my %testCases = (
        '1,234.56' => { wantMatch => 1, criteria => 'body' },
        '1.234,56' => { wantMatch => 1, criteria => 'body' },
        '1234.56'  => { wantMatch => 1, criteria => 'body' },
        '1234,56'  => { wantMatch => 1, criteria => 'body' },
        '1234.*'  => { wantMatch => 1, criteria => 'body' },
        '1234,*'  => { wantMatch => 1, criteria => 'body' },
        '1,234.*'  => { wantMatch => 1, criteria => 'body' },
        '1.234,*'  => { wantMatch => 1, criteria => 'body' },
        '1234.5*'  => { wantMatch => 1, criteria => 'body' },
        '1234,5*'  => { wantMatch => 1, criteria => 'body' },
        '1,234.5*'  => { wantMatch => 1, criteria => 'body' },
        '1.234,5*'  => { wantMatch => 1, criteria => 'body' },
        '1,234*'   => { wantMatch => 1, criteria => 'body' },
        '1.234*'   => { wantMatch => 1, criteria => 'body' },
        ' 1234.56'  => { wantMatch => 1, criteria => 'body' },
        ' 1234,56 '  => { wantMatch => 1, criteria => 'body' },
        '1234,56 '  => { wantMatch => 1, criteria => 'body' },
        '1,234'    => { wantMatch => 0, criteria => 'body' },
        '1.234'    => { wantMatch => 0, criteria => 'body' },
        '1,234,*'  => { wantMatch => 0, criteria => 'body' },
        '1,23,4.*' => { wantMatch => 0, criteria => 'body' },
        '1,234,567.89' => { wantMatch => 0, criteria => 'body' },
        '1.234.567,89' => { wantMatch => 0, criteria => 'body' },
        '1,234,56' => { wantMatch => 0, criteria => 'body' },
        '1.234.56' => { wantMatch => 0, criteria => 'body' },
        '9876' => { wantMatch => 1, criteria => 'text' },
    );

    foreach my $term (sort keys %testCases) {
        my $tc = $testCases{$term};

        my $res = $jmap->CallMethods([
            [ 'Email/query', {
                filter => {
                    $tc->{criteria} => $term,
                },
            }, 'R1'],
            [ 'SearchSnippet/get', {
                '#filter' => {
                    resultOf => 'R1',
                    name => 'Email/query',
                    path => '/filter',
                },
                '#emailIds' => {
                    resultOf => 'R1',
                    name => 'Email/query',
                    path => '/ids',
                },
            }, 'R2'],
        ]);
        $self->assert_num_equals($tc->{wantMatch}, scalar @{$res->[0][1]{ids}});

        if ($tc->{wantMatch} && $tc->{criteria} eq 'body') {
            $self->assert_str_equals(
                'The total amount is $<mark>1,234.56</mark> USD.',
                $res->[1][1]{list}[0]{preview});
        } else {
            $self->assert_null($res->[1][1]{list}[0]{preview});
        }

    }
}
