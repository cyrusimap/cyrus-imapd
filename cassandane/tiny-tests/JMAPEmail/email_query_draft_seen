#!perl
use Cassandane::Tiny;

sub test_email_query_draft_seen
    ($self)
{
    my $jmap = $self->{jmap};

    xlog $self, "create drafts mailbox";
    my $res = $jmap->CallMethods([
            ['Mailbox/set', { create => { "1" => {
                            name => "drafts",
                            parentId => undef,
                            role => "drafts"
             }}}, "R1"]
    ]);
    $self->assert_str_equals('Mailbox/set', $res->[0][0]);
    $self->assert_str_equals('R1', $res->[0][2]);
    $self->assert_not_null($res->[0][1]{created});
    my $drafts = $res->[0][1]{created}{"1"}{id};

    my $draft =  {
        mailboxIds =>  { $drafts => JSON::true },
        textBody => [{ partId => '1' }],
        bodyValues => { '1' => { value => "a flagged draft" }},
    };

    xlog $self, "Create a draft";
    $res = $jmap->CallMethods([['Email/set', {
        create => { "1" => $draft }
    }, "R1"]]);
    my $id = $res->[0][1]{created}{"1"}{id};

    xlog $self, "fetch emails without \$seen flag";
    $res = $jmap->CallMethods([['Email/query', {
        filter => {
            operator => "NOT",
            conditions => [ {allInThreadHaveKeyword => '$seen'} ]
        }
    }, "R1"]]);
    $self->assert_num_equals(1, scalar @{$res->[0][1]->{ids}});

    xlog $self, 'set \$draft flag on email';
    $res = $jmap->CallMethods([['Email/set', {
        update => {
            $id => {
                keywords => { '$draft' => JSON::true },
            },
        }
    }, "R1"]]);
    $self->assert(exists $res->[0][1]->{updated}{$id});

    xlog $self, "fetch emails without \$seen flag -- \$draft implies \$seen";
    $res = $jmap->CallMethods([['Email/query', {
        filter => {
            operator => "NOT",
            conditions => [ {allInThreadHaveKeyword => '$seen'} ]
        }
    }, "R1"]]);
    $self->assert_num_equals(0, scalar @{$res->[0][1]->{ids}});
}
