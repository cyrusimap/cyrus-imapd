%{
/* jmap_mail_props.h -- Lookup functions for JMAP Email properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      EMAIL_PROP_
%define word-array-name       email_prop_array
%define length-table-name     email_prop_lengths
%define hash-function-name    email_prop_hash
%define lookup-function-name  email_prop_lookup

jmap_property_t;

%%
"id",            NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"blobId",        NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"threadId",      NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"mailboxIds",    NULL, 0
"keywords",      NULL, 0
"size",          NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"receivedAt",    NULL, JMAP_PROP_IMMUTABLE
"headers",       NULL, JMAP_PROP_IMMUTABLE  | JMAP_PROP_SKIP_GET
"header:*",      NULL, JMAP_PROP_IMMUTABLE  | JMAP_PROP_SKIP_GET
"messageId",     NULL, JMAP_PROP_IMMUTABLE
"inReplyTo",     NULL, JMAP_PROP_IMMUTABLE
"references",    NULL, JMAP_PROP_IMMUTABLE
"sender",        NULL, JMAP_PROP_IMMUTABLE
"from",          NULL, JMAP_PROP_IMMUTABLE
"to",            NULL, JMAP_PROP_IMMUTABLE
"cc",            NULL, JMAP_PROP_IMMUTABLE
"bcc",           NULL, JMAP_PROP_IMMUTABLE
"replyTo",       NULL, JMAP_PROP_IMMUTABLE
"subject",       NULL, JMAP_PROP_IMMUTABLE
"sentAt",        NULL, JMAP_PROP_IMMUTABLE
"bodyStructure", NULL, JMAP_PROP_IMMUTABLE  | JMAP_PROP_SKIP_GET
"bodyValues",    NULL, JMAP_PROP_IMMUTABLE
"textBody",      NULL, JMAP_PROP_IMMUTABLE
"htmlBody",      NULL, JMAP_PROP_IMMUTABLE
"attachments",   NULL, JMAP_PROP_IMMUTABLE
"hasAttachment", NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"preview",       NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
#
# FM extensions (do ALL of these get through to Cyrus?)
"addedDates",            JMAP_MAIL_EXTENSION, 0
"removedDates",          JMAP_MAIL_EXTENSION, 0
"trustedSender",         JMAP_MAIL_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"spamScore",             JMAP_MAIL_EXTENSION, JMAP_PROP_IMMUTABLE
"calendarEvents",        JMAP_MAIL_EXTENSION, JMAP_PROP_IMMUTABLE
"previousCalendarEvent", JMAP_MAIL_EXTENSION, JMAP_PROP_IMMUTABLE
"isDeleted",             JMAP_MAIL_EXTENSION, 0
"imageSize",             JMAP_MAIL_EXTENSION, 0
"snoozed",               JMAP_MAIL_EXTENSION, 0
"bimiBlobId",            JMAP_MAIL_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"createdModseq",         JMAP_MAIL_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"deliveredTo",           JMAP_MAIL_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
%%

static const jmap_prop_hash_table_t jmap_email_props_map = {
    email_prop_array,
    EMAIL_PROP_TOTAL_KEYWORDS,
    EMAIL_PROP_MIN_HASH_VALUE,
    EMAIL_PROP_MAX_HASH_VALUE,
    &email_prop_lookup
};
