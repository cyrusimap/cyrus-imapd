%{
/* jmap_calendar_principal_props.h --
   Lookup functions for JMAP CalendarPrincipal properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      CALENDARPRINCIPAL_PROP_
%define word-array-name       calendarprincipal_prop_array
%define length-table-name     calendarprincipal_prop_lengths
%define hash-function-name    calendarprincipal_prop_hash
%define lookup-function-name  calendarprincipal_prop_lookup

jmap_property_t;

%%
"id",                 NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"name",               NULL, JMAP_PROP_SERVER_SET
"description",        NULL, JMAP_PROP_SERVER_SET
"email",              NULL, JMAP_PROP_SERVER_SET
"type",               NULL, JMAP_PROP_SERVER_SET
"timeZone",           NULL, 0,
"mayGetAvailability", NULL, JMAP_PROP_SERVER_SET
"accountId",          NULL, JMAP_PROP_SERVER_SET
"account",            NULL, JMAP_PROP_SERVER_SET
"sendTo",             NULL, JMAP_PROP_SERVER_SET
%%

static const jmap_prop_hash_table_t jmap_calendarprincipal_props_map = {
    calendarprincipal_prop_array,
    CALENDARPRINCIPAL_PROP_TOTAL_KEYWORDS,
    CALENDARPRINCIPAL_PROP_MIN_HASH_VALUE,
    CALENDARPRINCIPAL_PROP_MAX_HASH_VALUE,
    &calendarprincipal_prop_lookup
};
