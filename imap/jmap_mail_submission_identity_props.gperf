%{
/* jmap_mail_submission_identity_props.h --
   Lookup functions for JMAP Email Submission Identity properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      IDENTITY_PROP_
%define word-array-name       identity_prop_array
%define length-table-name     identity_prop_lengths
%define hash-function-name    identity_prop_hash
%define lookup-function-name  identity_prop_lookup

jmap_property_t;

%%
"id",            NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"name",          NULL, 0
"email",         NULL, JMAP_PROP_IMMUTABLE
"replyTo",       NULL, 0
"bcc",           NULL, 0
"textSignature", NULL, 0
"htmlSignature", NULL, 0
"mayDelete",     NULL, JMAP_PROP_SERVER_SET
#
# FM extensions (do ALL of these get through to Cyrus?)
"displayName",         JMAP_MAIL_EXTENSION, 0
"addBccOnSMTP",        JMAP_MAIL_EXTENSION, 0
"saveSentToMailboxId", JMAP_MAIL_EXTENSION, 0
"saveOnSMTP",          JMAP_MAIL_EXTENSION, 0
"useForAutoReply",     JMAP_MAIL_EXTENSION, 0
"isAutoConfigured",    JMAP_MAIL_EXTENSION, 0
"enableExternalSMTP",  JMAP_MAIL_EXTENSION, 0
"smtpServer",          JMAP_MAIL_EXTENSION, 0
"smtpPort",            JMAP_MAIL_EXTENSION, 0
"smtpSSL",             JMAP_MAIL_EXTENSION, 0
"smtpUser",            JMAP_MAIL_EXTENSION, 0
"smtpPassword",        JMAP_MAIL_EXTENSION, 0
"smtpRemoteService",   JMAP_MAIL_EXTENSION, 0
"popLinkId",           JMAP_MAIL_EXTENSION, 0
%%

static const jmap_prop_hash_table_t jmap_identity_props_map = {
    identity_prop_array,
    IDENTITY_PROP_TOTAL_KEYWORDS,
    IDENTITY_PROP_MIN_HASH_VALUE,
    IDENTITY_PROP_MAX_HASH_VALUE,
    &identity_prop_lookup
};
