%{
/* jmap_blob_props.h -- Lookup functions for JMAP Blob properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      BLOB_PROP_
%define word-array-name       blob_prop_array
%define length-table-name     blob_prop_lengths
%define hash-function-name    blob_prop_hash
%define lookup-function-name  blob_prop_lookup

jmap_property_t;

%%
"data",           NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"data:asBase64",  NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"data:asText",    NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"digest:md5",     NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"digest:sha",     NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"digest:sha-256", NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_SKIP_GET
"size",           NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
%%

static const jmap_prop_hash_table_t jmap_blob_props_map = {
    blob_prop_array,
    BLOB_PROP_TOTAL_KEYWORDS,
    BLOB_PROP_MIN_HASH_VALUE,
    BLOB_PROP_MAX_HASH_VALUE,
    &blob_prop_lookup
};
