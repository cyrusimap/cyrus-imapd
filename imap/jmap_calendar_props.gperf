%{
/* jmap_calendar_props.h -- Lookup functions for JMAP Calendar properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      CALENDAR_PROP_
%define word-array-name       calendar_prop_array
%define length-table-name     calendar_prop_lengths
%define hash-function-name    calendar_prop_hash
%define lookup-function-name  calendar_prop_lookup

jmap_property_t;

%%
"id",                       NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"name",                     NULL, 0
"description",              NULL, 0
"color",                    NULL, 0
"sortOrder",                NULL, 0
"isVisible",                NULL, 0
"isSubscribed",             NULL, 0
"includeInAvailability",    NULL, 0
"defaultAlertsWithTime",    NULL, 0
"defaultAlertsWithoutTime", NULL, 0
"timeZone",                 NULL, 0
"participantIdentities",    NULL, 0
"shareWith",                NULL, 0
"myRights",                 NULL, JMAP_PROP_SERVER_SET
#
# FM extensions (do ALL of these get through to Cyrus?)
"syncedFrom",       JMAP_CALENDARS_EXTENSION, JMAP_PROP_EXTERNAL
"isEventsPublic",   JMAP_CALENDARS_EXTENSION, JMAP_PROP_EXTERNAL
"isFreeBusyPublic", JMAP_CALENDARS_EXTENSION, JMAP_PROP_EXTERNAL
"eventsUrl",        JMAP_CALENDARS_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_EXTERNAL
"freeBusyUrl",      JMAP_CALENDARS_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_EXTERNAL
"calDavUrl",        JMAP_CALENDARS_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_EXTERNAL
"mailboxUniqueId",  JMAP_CALENDARS_EXTENSION, JMAP_PROP_SERVER_SET
"x-href",           JMAP_DEBUG_EXTENSION,     JMAP_PROP_SERVER_SET
%%

static const jmap_prop_hash_table_t jmap_calendar_props_map = {
    calendar_prop_array,
    CALENDAR_PROP_TOTAL_KEYWORDS,
    CALENDAR_PROP_MIN_HASH_VALUE,
    CALENDAR_PROP_MAX_HASH_VALUE,
    &calendar_prop_lookup
};
