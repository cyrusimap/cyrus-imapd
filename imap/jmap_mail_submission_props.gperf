%{
/* jmap_mail_submission_props.h --
   Lookup functions for JMAP Email Submission properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      SUBMISSION_PROP_
%define word-array-name       submission_prop_array
%define length-table-name     submission_prop_lengths
%define hash-function-name    submission_prop_hash
%define lookup-function-name  submission_prop_lookup

jmap_property_t;

%%
"id",             NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"identityId",     NULL, JMAP_PROP_IMMUTABLE
"emailId",        NULL, JMAP_PROP_IMMUTABLE
"threadId",       NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"envelope",       NULL, JMAP_PROP_IMMUTABLE
"sendAt",         NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
"undoStatus",     NULL, JMAP_PROP_SERVER_SET
"deliveryStatus", NULL, JMAP_PROP_SERVER_SET
"dsnBlobIds",     NULL, JMAP_PROP_SERVER_SET
"mdnBlobIds",     NULL, JMAP_PROP_SERVER_SET
#
# FM extensions
"onSend",  JMAP_MAIL_EXTENSION, JMAP_PROP_IMMUTABLE
"created", JMAP_MAIL_EXTENSION, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE
%%

static const jmap_prop_hash_table_t jmap_submission_props_map = {
    submission_prop_array,
    SUBMISSION_PROP_TOTAL_KEYWORDS,
    SUBMISSION_PROP_MIN_HASH_VALUE,
    SUBMISSION_PROP_MAX_HASH_VALUE,
    &submission_prop_lookup
};
