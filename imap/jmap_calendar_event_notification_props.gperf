%{
/* jmap_calendar_event_notification_props.h --
   Lookup functions for JMAP CalendarEventNotification properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      EVENTNOTIFICATION_PROP_
%define word-array-name       eventnotification_prop_array
%define length-table-name     eventnotification_prop_lengths
%define hash-function-name    eventnotification_prop_hash
%define lookup-function-name  eventnotification_prop_lookup

jmap_property_t;

%%
"id",              NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"created",         NULL, JMAP_PROP_SERVER_SET
"changedBy",       NULL, JMAP_PROP_SERVER_SET
"comment",         NULL, JMAP_PROP_SERVER_SET
"type",            NULL, JMAP_PROP_SERVER_SET
"calendarEventId", NULL, 0,
"isDraft",         NULL, JMAP_PROP_SERVER_SET
"event",           NULL, JMAP_PROP_SERVER_SET
"eventPatch",      NULL, JMAP_PROP_SERVER_SET
%%

static const jmap_prop_hash_table_t jmap_calendareventnotification_props_map = {
    eventnotification_prop_array,
    EVENTNOTIFICATION_PROP_TOTAL_KEYWORDS,
    EVENTNOTIFICATION_PROP_MIN_HASH_VALUE,
    EVENTNOTIFICATION_PROP_MAX_HASH_VALUE,
    &eventnotification_prop_lookup
};
