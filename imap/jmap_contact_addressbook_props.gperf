%{
/* jmap_contact_addressbook_props.h --
   Lookup functions for JMAP AddressBook properties */
/* SPDX-License-Identifier: BSD-3-Clause-CMU */
/* See COPYING file at the root of the distribution for more details. */

%}

%enum
%null-strings
%global-table
%readonly-tables
%compare-lengths
%struct-type
%omit-struct-type

%define slot-name             name
%define initializer-suffix    ,NULL,0
%define constants-prefix      ADDRESSBOOK_PROP_
%define word-array-name       addressbook_prop_array
%define length-table-name     addressbook_prop_lengths
%define hash-function-name    addressbook_prop_hash
%define lookup-function-name  addressbook_prop_lookup

jmap_property_t;

%%
"id",           NULL, JMAP_PROP_SERVER_SET | JMAP_PROP_IMMUTABLE | JMAP_PROP_ALWAYS_GET
"name",         NULL, JMAP_PROP_MANDATORY
"description",  NULL, 0
"sortOrder",    NULL, 0
"isDefault",    NULL, JMAP_PROP_SERVER_SET
"isSubscribed", NULL, 0
"shareWith",    NULL, 0
"myRights",     NULL, JMAP_PROP_SERVER_SET
#
# FM extensions (do ALL of these get through to Cyrus?)
"cyrusimap.org:href", JMAP_CONTACTS_EXTENSION, JMAP_PROP_SERVER_SET
%%

static const jmap_prop_hash_table_t jmap_addressbook_props_map = {
    addressbook_prop_array,
    ADDRESSBOOK_PROP_TOTAL_KEYWORDS,
    ADDRESSBOOK_PROP_MIN_HASH_VALUE,
    ADDRESSBOOK_PROP_MAX_HASH_VALUE,
    &addressbook_prop_lookup
};
